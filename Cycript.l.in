/* Cycript - Optimizing JavaScript Compiler/Runtime
 * Copyright (C) 2009-2010  Jay Freeman (saurik)
*/

/* GNU Lesser General Public License, Version 3 {{{ */
/*
 * Cycript is free software: you can redistribute it and/or modify it under
 * the terms of the GNU Lesser General Public License as published by the
 * Free Software Foundation, either version 3 of the License, or (at your
 * option) any later version.
 *
 * Cycript is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 * FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public
 * License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with Cycript.  If not, see <http://www.gnu.org/licenses/>.
**/
/* }}} */

/* XXX: supposedly I will be screwed on very very long multi-line comments and need to replace these with a manual lexer. http://websrv.cs.fsu.edu/~engelen/courses/COP5621/Pr2.pdf */

%{
#define YYLTYPE cy::location
#include "Cycript.tab.hh"
typedef cy::parser::token tk;

#define YY_EXTRA_TYPE CYDriver *

#define A new($pool)
#define Y apr_pstrmemdup($pool, yytext, yyleng)

#define I(type, Type, Name) do { \
    yylval->type ## _ = A CY ## Type; \
    return tk::Name; \
} while (false)

#define T yylval->newline_ = yyextra->state_ == CYNewLine; BEGIN(Div);
#define C T yyextra->state_ = CYClear;
#define R T yyextra->state_ = CYRestricted;

#define E(prefix) L C { \
    char *value(A char[yyleng + sizeof(prefix)]); \
    memcpy(value, prefix, sizeof(prefix) - 1); \
    memcpy(value + sizeof(prefix) - 1, yytext, yyleng); \
    value[yyleng + sizeof(prefix) - 1] = '\0'; \
    I(literal, RegEx(value), RegularExpressionLiteral); \
}

#define N \
    if (yyextra->state_ != CYNewLine) { \
        if (yyextra->state_ != CYRestricted) \
            yyextra->state_ = CYNewLine; \
        else { \
            yyextra->state_ = CYClear; \
            return tk::NewLine; \
        } \
    }

#define V(more) { \
    if (const char *nl = reinterpret_cast<const char *>(memchr(yytext, '\n', yyleng))) { \
        unsigned lines(0); \
        size_t left; \
        do { \
            ++lines; \
            left = yyleng - (nl - yytext) - 1; \
            nl = reinterpret_cast<const char *>(memchr(nl + 1, '\n', left)); \
        } while (nl != NULL); \
        yylloc->end.lines(lines); \
        yylloc->end.columns(left); \
        yylloc->step(); \
        more \
    } else L \
}

#define L { \
    yylloc->step(); \
    yylloc->columns(yyleng); \
}

int H(char c) {
    if (c >= '0' && c <= '9')
        return c - '0';
    if (c >= 'a' && c <= 'f')
        return c - 'a' + 10;
    if (c >= 'A' && c <= 'F')
        return c - 'A' + 10;
    return -1;
}

#define YY_INPUT(data, value, size) { \
    if (yyextra->file_ != NULL) { \
        size_t copy(fread(data, 1, size, yyextra->file_)); \
        value = copy == 0 ? YY_NULL : copy; \
    } else if (yyextra->size_ == 0) \
        value = YY_NULL; \
    else { \
        size_t copy(size); \
        copy = (std::min(copy, yyextra->size_)); \
        memcpy(data, yyextra->data_, copy); \
        yyextra->data_ += copy; \
        yyextra->size_ -= copy; \
        value = copy; \
    } \
}

%}

%option prefix="cy"
%option bison-bridge
%option bison-locations
%option noyywrap
%option yylineno
%option nounput
%option interactive
%option reentrant
%option stack

Exponent [eE][+-]?[0-9]+
Escape   \\[\\'"bfnrtv]|\\0|\\x[0-9a-fA-F]{2}|\\u[0-9a-fA-F]{4}|\\\n

IdentifierStart [a-zA-Z$_]
IdentifierPart  [a-zA-Z$_0-9]

NonTerminator [^\n]
BackslashSequence \\{NonTerminator}
RegularExpressionFirstChar [^\n*\\/]|{BackslashSequence}
RegularExpressionChar [^\n\\/]|{BackslashSequence}
RegularExpressionFlags {IdentifierPart}*
RegularExpressionChars {RegularExpressionChar}*
RegularExpressionBody {RegularExpressionFirstChar}{RegularExpressionChars}

@begin E4X
XMLNameStart [a-zA-Z_:]
XMLNamePart [a-zA-Z0-9.-_:]
XMLName {XMLNameStart}{XMLNamePart}*
@end

%s Div
%s RegExp

@begin E4X
%x XMLContent
%x XMLTag
@end

%%

<RegExp>\/{RegularExpressionBody}\/{RegularExpressionFlags} E("")

\/\/[^\n]* L

	/* http://ostermiller.org/findcomment.html */
	/* XXX: unify these two rules using !? */
\/\*!([^*]|[\r\n]|(\*+([^*/]|[\r\n])))*\*+\/ V() C I(comment, Comment(Y), Comment);
\/\*([^*]|[\r\n]|(\*+([^*/]|[\r\n])))*\*+\/ V(N)

@begin E4X
<RegExp>"<>"      L return tk::LeftRight;
<XMLContent>"</>" L return tk::LeftSlashRight;

<RegExp,XMLContent>\<!\[CDATA\[(\n|[^[]|\[[^[]|\[\[[^>])*]]> V() return tk::XMLCDATA;
<RegExp,XMLContent>\<!--(\n|[^-]|-[^-])*--> V() return tk::XMLComment;
<RegExp,XMLContent>\<?(\n|[^?]|\?[^>])*?> V() return tk::XMLPI;

<XMLTag>"="  L return tk::Equal;
<XMLTag>">"  L return tk::Right;
<XMLTag>"/>" L return tk::SlashRight;
<XMLTag>"{"  L return tk::OpenBrace;

<XMLTag>\"(\n|[^"])*\"|'(\n|[^'])*' V() return tk::XMLAttributeValue;
<XMLTag>{XMLName} L return tk::XMLName;
<XMLTag>[ \t\r\n] V() return tk::XMLWhitespace;

<XMLContent>"{"  L return tk::OpenBrace;
<XMLContent>"<"  L return tk::Left;
<XMLContent>"</" L return tk::LeftSlash;
@end

@begin E4X
"::"   L C return tk::ColonColon;
".."   L C return tk::PeriodPeriod;
@end

@begin E4X ObjectiveC
"@"    L C return tk::At;
@end

"&"    L C return tk::Ampersand;
"&&"   L C return tk::AmpersandAmpersand;
"&="   L C return tk::AmpersandEqual;
"^"    L C return tk::Carrot;
"^="   L C return tk::CarrotEqual;
"="    L C return tk::Equal;
"=="   L C return tk::EqualEqual;
"==="  L C return tk::EqualEqualEqual;
"!"    L C return tk::Exclamation;
"!="   L C return tk::ExclamationEqual;
"!=="  L C return tk::ExclamationEqualEqual;
"-"    L C return tk::Hyphen;
"-="   L C return tk::HyphenEqual;
"--"   L C return yylval->newline_ ? tk::HyphenHyphen_ : tk::HyphenHyphen;
"->"   L C return tk::HyphenRight;
"<"    L C return tk::Left;
"<="   L C return tk::LeftEqual;
"<<"   L C return tk::LeftLeft;
"<<="  L C return tk::LeftLeftEqual;
"%"    L C return tk::Percent;
"%="   L C return tk::PercentEqual;
"."    L C return tk::Period;
"|"    L C return tk::Pipe;
"|="   L C return tk::PipeEqual;
"||"   L C return tk::PipePipe;
"+"    L C return tk::Plus;
"+="   L C return tk::PlusEqual;
"++"   L C return yylval->newline_ ? tk::PlusPlus_ : tk::PlusPlus;
">"    L C return tk::Right;
">="   L C return tk::RightEqual;
">>"   L C return tk::RightRight;
">>="  L C return tk::RightRightEqual;
">>>"  L C return tk::RightRightRight;
">>>=" L C return tk::RightRightRightEqual;
"*"    L C return tk::Star;
"*="   L C return tk::StarEqual;
"~"    L C return tk::Tilde;

<Div>"/"  L C return tk::Slash;
<Div>"/=" L C return tk::SlashEqual;

":"    L C return tk::Colon;
","    L C return tk::Comma;
"?"    L C return tk::Question;
";"    L C return tk::SemiColon;

"("    L C return tk::OpenParen;
")"    L C return tk::CloseParen;

"{"    L C return yylval->newline_ ? tk::OpenBrace_ : tk::OpenBrace;
"}"    L C return tk::CloseBrace;

"["    L C return tk::OpenBracket;
"]"    L C return tk::CloseBracket;

@begin Java
"@class"          L C return tk::AtClass;
@end

@begin ObjectiveC
"@end"            L C return tk::AtEnd;
"@implementation" L C return tk::AtImplementation;
"@import"         L C return tk::AtImport;
"@selector"       L C return tk::AtSelector;
@end

"false"        L C I(false, False(), False);
"null"         L C I(null, Null(), Null);
"true"         L C I(true, True(), True);

"break"        L R I(word, Word("break"), Break);
"case"         L C I(word, Word("case"), Case);
"catch"        L C I(word, Word("catch"), Catch);
"continue"     L R I(word, Word("continue"), Continue);
"default"      L C I(word, Word("default"), Default);
"delete"       L C I(word, Word("delete"), Delete);
"do"           L C I(word, Word("do"), Do);
"else"         L C I(word, Word("else"), Else);
"finally"      L C I(word, Word("finally"), Finally);
"for"          L C I(word, Word("for"), For);
"function"     L C I(word, Word("function"), Function);
"if"           L C I(word, Word("if"), If);
"in"           L C I(word, Word("in"), In);
"instanceof"   L C I(word, Word("instanceof"), InstanceOf);
"new"          L C I(word, Word("new"), New);
"return"       L R I(word, Word("return"), Return);
"switch"       L C I(word, Word("switch"), Switch);
"this"         L C I(this, This(), This);
"throw"        L R I(word, Word("throw"), Throw);
"try"          L C I(word, Word("try"), Try);
"typeof"       L C I(word, Word("typeof"), TypeOf);
"var"          L C I(word, Word("var"), Var);
"void"         L C I(word, Word("void"), Void);
"while"        L C I(word, Word("while"), While);
"with"         L C I(word, Word("with"), With);

"debugger"     L C I(word, Word("debugger"), Debugger);

"const"        L C I(word, Word("const"), Const);

"class"        L C I(word, Word("class"), Class);
"enum"         L C I(word, Word("enum"), Enum);
"export"       L C I(word, Word("export"), Export);
"extends"      L C I(word, Word("extends"), Extends);
"import"       L C I(word, Word("import"), Import);
"super"        L C I(word, Word("super"), Super);

"implements"   L C I(identifier, Identifier("implements"), Implements);
"interface"    L C I(identifier, Identifier("interface"), Interface);
"package"      L C I(identifier, Identifier("package"), Package);
"private"      L C I(identifier, Identifier("private"), Private);
"protected"    L C I(identifier, Identifier("protected"), Protected);
"public"       L C I(identifier, Identifier("public"), Public);
"static"       L C I(identifier, Identifier("static"), Static);

"abstract"     L C I(identifier, Identifier("abstract"), Abstract);
"boolean"      L C I(identifier, Identifier("boolean"), Boolean);
"byte"         L C I(identifier, Identifier("byte"), Byte);
"char"         L C I(identifier, Identifier("char"), Char);
"double"       L C I(identifier, Identifier("double"), Double);
"final"        L C I(identifier, Identifier("final"), Final);
"float"        L C I(identifier, Identifier("float"), Float);
"goto"         L C I(identifier, Identifier("goto"), Goto);
"int"          L C I(identifier, Identifier("int"), Int);
"long"         L C I(identifier, Identifier("long"), Long);
"native"       L C I(identifier, Identifier("native"), Native);
"short"        L C I(identifier, Identifier("short"), Short);
"synchronized" L C I(identifier, Identifier("synchronized"), Synchronized);
"throws"       L C I(identifier, Identifier("throws"), Throws);
"transient"    L C I(identifier, Identifier("transient"), Transient);
"volatile"     L C I(identifier, Identifier("volatile"), Volatile);

"let"          L C I(identifier, Identifier("let"), Let);
"yield"        L C I(identifier, Identifier("yield"), Yield);

"each"         L C I(identifier, Identifier("each"), Each);

@begin E4X
"namespace"    L C I(identifier, Identifier("namespace"), Namespace);
"xml"          L C I(identifier, Identifier("xml"), XML);
@end

{IdentifierStart}{IdentifierPart}* L C I(identifier, Identifier(Y), Identifier_);

(\.[0-9]+|(0|[1-9][0-9]*)(\.[0-9]*)?){Exponent}? L C I(number, Number(strtod(yytext, NULL)), NumericLiteral);

0[xX][0-9a-fA-F]+ L C I(number, Number(strtoull(yytext + 2, NULL, 16)), NumericLiteral);
0[0-7]+ L C I(number, Number(strtoull(yytext + 1, NULL, 8)), NumericLiteral);
0[bB][0-1]+ L C I(number, Number(strtoull(yytext + 2, NULL, 2)), NumericLiteral);

\"([^"\\\n]|{Escape})*\"|'([^'\\\n]|{Escape})*' L C {
    char *value(A char[yyleng]);
    char *local(value);

    for (yy_size_t i(1), e(yyleng - 1); i != e; ++i) {
        char next(yytext[i]);

        if (yytext[i] == '\\')
            switch (next = yytext[++i]) {
                case '\n': continue;
                case '\\': next = '\\'; break;
                case '\'': next = '\''; break;
                case '"': next = '"'; break;
                case 'b': next = '\b'; break;
                case 'f': next = '\f'; break;
                case 'n': next = '\n'; break;
                case 'r': next = '\r'; break;
                case 't': next = '\t'; break;
                case 'v': next = '\v'; break;
                case '0': next = '\0'; break;

                case 'x':
                    next = H(yytext[i + 1]) << 4 | H(yytext[i + 2]);
                    i += 2;
                break;
            }

        *local++ = next;
    }

    *local = '\0';
    I(string, String(value, local - value), StringLiteral);
}

\r?\n yylloc->end.lines(); yylloc->step(); N

[ \t] L

<<EOF>> if (yyextra->auto_) { yyextra->auto_ = false; return tk::AutoComplete; } L yyterminate();

. L {
    CYDriver::Error error;
    error.location_ = *yylloc;
    error.message_ = "syntax error, unknown token";
    yyextra->errors_.push_back(error);
    yyterminate();
}

%%

void CYDriver::ScannerInit() {
    cylex_init(&scanner_);
    cyset_extra(this, scanner_);
}

void CYDriver::ScannerDestroy() {
    cylex_destroy(scanner_);
}

CYDriver::Condition CYDriver::GetCondition() {
    switch (yy_top_state(scanner_)) {
        case RegExp:
            return RegExpCondition;
@begin E4X
        case XMLContent:
            return XMLContentCondition;
        case XMLTag:
            return XMLTagCondition;
@end
        default:
            _assert(false);
    }
}

void CYDriver::SetCondition(Condition condition) {
    struct yyguts_t *yyg(reinterpret_cast<struct yyguts_t *>(scanner_));

    switch (condition) {
        case RegExpCondition:
            BEGIN(RegExp);
            break;
@begin E4X
        case XMLContentCondition:
            BEGIN(XMLContent);
            break;
        case XMLTagCondition:
            BEGIN(XMLTag);
            break;
@end
        default:
            _assert(false);
    }
}

void CYDriver::PushCondition(Condition condition) {
    switch (condition) {
        case RegExpCondition:
            yy_push_state(RegExp, scanner_);
            break;
@begin E4X
        case XMLContentCondition:
            yy_push_state(XMLContent, scanner_);
            break;
        case XMLTagCondition:
            yy_push_state(XMLTag, scanner_);
            break;
@end
        default:
            _assert(false);
    }
}

void CYDriver::PopCondition() {
    yy_pop_state(scanner_);
}
